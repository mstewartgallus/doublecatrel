embed
{{ coq
Require Blech.Map.
}}

metavar var, x, y ::= {{ coq nat }} {{ coq-equality }} {{ lex alphanum}} {{ tex \mathit{[[var]]} }}

grammar
type, t :: 't_' ::=
  | unit         ::   :: unit
  | t * t'       ::   :: prod

context, E :: 'E_' ::=
  | x                        ::   :: var
  | lam x : t . E            ::   :: lam (+ bind x in E +)
  | E E'                     ::   :: app
  | tt                       ::   :: tt
  | E ; E'                   ::   :: step
  | E , E'                   ::   :: fanout
  | let ( x , y ) := E in E' ::   :: let (+ bind x union y in E' +)
  | ( E )                    :: S :: paren {{ icho ([[E]]) }}

term, v :: 'v_' ::=
  | x            ::   :: var
  | tt           ::   :: tt
  | fst v        ::   :: fst
  | snd v        ::   :: snd
  | v , v'       ::   :: fanout
  | ( v )        :: S :: paren {{ icho ([[v]]) }}

normal, N :: 'N_' ::=
  | tt      ::   :: tt
  | N , N'  ::   :: fanout
  | ( N )   :: S :: paren {{ coq ([[N]]) }}

linear, D {{ tex \Delta }} :: 'D_' ::= {{ coq (Map.map type) }} {{ coq-universe Type }}
  | nil           :: M :: nil {{ coq Map.empty }}
  | D , x : t     :: M :: add {{ coq (Map.add [[x]] [[t]] [[D]]) }}
  | D ++ D'       :: M :: merge {{ coq (Map.merge [[D]] [[D']]) }}

environment, G {{ tex \Gamma }} :: 'G_' ::= {{ coq (list (var * type)) }}
  | nil         ::   :: nil {{ coq nil }}
  | G , x : t     ::   :: add {{ coq (cons ([[x]], [[t]]) [[G]]) }}

store, S {{ tex \sigma }} :: 'S_'  ::= {{ coq (Map.map normal) }} {{ coq-universe Type }}
  | nil                   :: M :: nil {{ coq Map.empty }}
  | S , x := N            :: M :: add {{ coq (Map.add [[x]] [[N]] [[S]]) }}
  | S ++ S'               :: M :: merge {{ coq (Map.merge [[S]] [[S']]) }}

span, P :: 'P_' ::= {{ coq-universe Type }}
  | S |- N  ::   :: with

set, X {{ tex P^* }} :: 'X_' ::= {{ coq (list span) }} {{ coq-universe Type }}
  | nil          ::   :: nil {{ coq nil }}
  | X , P        ::   :: cons {{ coq (cons [[P]] [[X]]) }}


terminals :: 'terminals_' ::=
  | :            ::   :: in     {{ tex \colon{} }}
  | |-           ::   :: turnstile   {{ tex \vdash{} }}
  | :=           ::   :: update  {{ tex \mathbin{:=} }}
  | ++           ::   :: merge   {{ tex \cup{} }}
  | unit         ::   :: unit  {{ tex \top{} }}
  | *            ::   :: prod  {{ tex \times{} }}
  | tt           ::   :: tt   {{ tex \mathop{ \textbf ! } }}
  | fst          ::   :: fst  {{ tex \pi{}_1 }}
  | snd          ::   :: snd  {{ tex \pi{}_2 }}
  | lam          ::   :: lam  {{ tex \lambda{} }}
  | nil          ::   :: nil   {{ tex \bullet{} }}
  | !            ::   :: big     {{ tex \mathrel{\Downarrow} }}

substitutions
  single v x :: subst
  single E x :: subst 

% subrules are awkward
funs
  toterm ::=
fun
  [ N ] :: v :: toterm {{ tex \llbracket [[N]] \rrbracket }}
by
  [ tt ] === tt
  [ N, N' ] === [ N ], [ N' ]

embed
{{ coq

Coercion toterm: normal >-> term.

Definition find (x: var) (Î“: list (var * type)): option type :=
  match List.find (fun '(k, _) => if eq_var x k then true else false) Î“ with
  | Some (_, t) => Some t
  | _ => None
  end.
}}
defns

judge_context :: 'J' ::=

defn
D |- E : t ::   :: E :: 'E_'
by

------------ :: var
nil , x: t |- x: t

D , x: t1 |- E: t2
-------------------------- :: abs
D |- lam x: t1 . E: t1 * t2

D1 |- E1: t1 * t2
D2 |- E2: t1
------------------ :: app
D1 ++ D2 |- E1 E2: t2

------------------- :: tt
nil |- tt: unit

D1 |- E1: unit
D2 |- E2: t
------------------- :: step
D1 ++ D2 |- E1; E2: t

D1 |- E1: t1
D2 |- E2: t2
------------------- :: fanout
D1 ++ D2 |- E1, E2: t1 * t2

D1 |- E1 : t1 * t2
D2, x0: t1, x1: t2 |- E2: t3
------------------- :: let
D1 ++ D2 |- let (x0, x1) := E1 in E2: t3

defns

judge_term :: 'J' ::=

defn
G |- v : t ::   :: v :: 'v_'
by

{{ find [[x]] [[G]] = Some [[t]] }}
------------ :: var
G |- x: t

------------ :: tt
G |- tt: unit

G |- v1: t1
G |- v2: t2
------------------ :: fanout
G |- v1, v2: t1 * t2

G |- v: t1 * t2
------------------ :: fst
G |- fst v: t1

G |- v: t1 * t2
------------------ :: snd
G |- snd v: t2

defns

big :: '' ::=

defn
v ! N ::   :: big :: 'big_'
by

-------------------- :: tt
tt ! tt

v1 ! N1'
v2 ! N2'
-------------------- :: fanout
(v1, v2) ! (N1', N2')

v ! N1, N2
-------------------- :: fst
fst v ! N1

v ! N1, N2
------------------- :: snd
snd v ! N2

defns

sat :: '' ::=

defn
E ! S |- N ::   :: sat :: 'sat_'
by

---------------------- :: var
x ! nil, x := N |- N

------------------ :: tt
tt ! nil |- tt

E ! S |- tt 
E' ! S' |- N 
-------------------------- :: step
(E; E') !  S ++ S' |- N 

E !  S |- N 
E' !  S' |- N' 
-------------------------- :: fanout
(E, E') !  S ++ S' |- N, N'

E ! S |- N0, N1 
E' ! S', x := N0, y := N1 |- N2 
------------------------------------------- :: let
(let (x, y) := E in E') ! S ++ S' |- N2 

E ! S, x := N |- N' 
------------------------------- :: lam
(lam x: t. E) ! S |- N, N' 

E ! S |- N, N' 
E' ! S' |- N 
------------------------- :: app
E E' ! S ++ S' |- N' 

defns

sound :: '' ::=

defn
E ! X ::   :: sound :: 'sound_'
by

------- :: nil
E ! nil

E ! X
E ! S |- N
-------- :: cons
E ! X, S |- N
