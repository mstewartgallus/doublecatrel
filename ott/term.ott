embed
{{ coq-preamble
Require Blech.Assoc.
}}
metavar var, x, y ::= {{ coq nat }} {{ coq-equality }} {{ lex alphanum}} {{ tex \mathit{[[var]]} }}
metavar sort, A ::= {{ coq nat }} {{ coq-equality }} {{ lex alphanum}} {{ tex \mathit{[[sort]]} }}
metavar function, f ::= {{ coq nat }} {{ coq-equality }} {{ lex alphanum}} {{ tex \mathit{[[function]]} }}
metavar relation, R ::= {{ coq nat }} {{ coq-equality }} {{ lex alphanum}} {{ tex \mathit{[[relation]]} }}

grammar
type, t {{ tex \tau }} {{ coq τ }} :: 't_' ::= {{ coq-equality }}
  | A            ::   :: var
  | unit         ::   :: unit
  | t * t'       ::   :: prod
  | ( t )        :: S :: paren {{ coq ([[t]]) }}

elim, V :: 'V_' ::= {{ coq-equality }}
  | x              ::   :: var
  | fst V          ::   :: fst
  | snd V          ::   :: snd
  | ( V )          :: S :: paren {{ icho ([[V]]) }}
intro, v :: 'v_' ::= {{ coq-equality }}
  | f v           ::   :: function
  | tt            ::   :: tt
  | v , v'        ::   :: fanout
  | neu ( V )     ::   :: neu {{ tex [ [[V]] ] }}
  | ( v )         :: S :: paren {{ icho ([[v]]) }}

context, E :: 'E_' ::=
  | x                        ::   :: var
  | f E                      ::   :: function
  | epsilon x . c            ::   :: epsilon (+ bind x in c +)

  | tt                       ::   :: tt
  | E , E'                   ::   :: fanout

  | epsilon . c              ::   :: match_tt
  | epsilon x , y . c        ::   :: match_fanout (+ bind x union y in c +)

  | dup E                    ::   :: dup
  | del E : t                ::   :: del
  | ( E )                    :: S :: paren {{ icho ([[E]]) }}
command, c :: 'c_' ::=
  | R E                      ::   :: relation
  | E = E' : t               ::   :: unify

  | false                    ::   :: false
  | c \/ c'                  ::   :: or

  | true                     :: M :: true {{ coq (c_unify E_tt E_tt t_unit) }}
  | c /\ c'                  :: M :: and {{ coq (c_unify (E_match_tt [[c]]) (E_match_tt [[c']]) t_unit) }}

  | ( c )                    :: S :: paren {{ icho ([[c]]) }}


use, u :: 'u_' ::= {{ coq-equality }}
  | used   ::  :: used
  | unused ::  :: unused

usage, D {{ tex \Delta }} {{ coq Δ }} :: 'D_' ::= {{ coq (Assoc.assoc var use) }}
  | nil                  ::   :: nil {{ coq nil }}
  | D , x [ u ]          ::   :: cons {{ coq (cons (Assoc.maps [[x]] [[u]]) [[D]]) }}

sorts, S :: 'S_' ::= {{ coq (Assoc.assoc var unit) }}
  | nil          ::   :: nil {{ coq nil }}
  | S , A : sort ::   :: add {{ coq (cons (Assoc.maps [[A]] tt) [[S]]) }}

relations, RS {{ tex R^* }} :: 'RS_' ::= {{ coq (Assoc.assoc relation type) }}
  | nil        ::   :: nil {{ coq nil }}
  | RS , R : t ::   :: add {{ coq (cons (Assoc.maps [[R]] [[t]]) [[RS]]) }}

functions, FS {{ tex f^* }} :: 'FS_' ::= {{ coq (Assoc.assoc function (type * sort)) }}
  | nil             ::   :: nil {{ coq nil }}
  | FS , f : t -> A ::   :: add {{ coq (cons (Assoc.maps [[f]] ([[t]], [[A]])) [[FS]]) }}

environment, G {{ tex \Gamma }} {{ coq Γ }} :: 'G_' ::= {{ coq (Assoc.assoc var type) }}
  | nil         ::   :: nil {{ coq nil }}
  | G , x : t   ::   :: add {{ coq (cons (Assoc.maps [[x]] [[t]]) [[G]]) }}
  | ( G )       :: S :: paren {{ coq ([[G]]) }}

% FIXME bind G?
sequent, H :: 'H_' ::=
  | Pi G . c => c' ::  :: seq

theory, T :: 'T_' ::= {{ coq (list sequent) }}
  | nil         ::   :: nil {{ coq nil }}
  | T , H       ::   :: add {{ coq (cons [[H]] [[T]]) }}

subst, p {{ tex \rho }} {{ coq ρ }} :: 'p_'  ::= {{ coq (Assoc.assoc var intro) }}
  | nil                   :: M :: nil {{ coq nil }}
  | p , x := v            :: M :: add {{ coq (cons (Assoc.maps [[x]] [[v]]) [[p]]) }}
  | ( p )                 :: S :: paren {{ coq ([[p]]) }}

formula :: formula_ ::=  
 | judgement                        ::   :: judgement
 | x != y                           :: M :: neqx {{ icho ([[x]] <> [[y]]) }}
 | x = y                            :: M :: eqx {{ icho ([[x]] = [[y]]) }}

terminals :: 'terminals_' ::=
  | :            ::   :: oftype {{ tex \colon{} }}
  | |-           ::   :: entails  {{ tex \vdash{} }}
  | :=           ::   :: update  {{ tex \mathbin{:=} }}
  | ->           ::   :: function  {{ tex \rightarrow }}
  | =>           ::   :: sequent  {{ tex \Rightarrow }}
  | Pi           ::   :: Pi  {{ tex \Pi }}
  | unit         ::   :: unit  {{ tex \textbf{I} }}
  | sort         ::   :: sort  {{ tex \Box }}
  | *            ::   :: prod  {{ tex \otimes{} }}
  | used         ::   :: used  {{ tex \textbf{s} }}
  | unused       ::   :: unused  {{ tex \textbf{f} }}
  | #            ::   :: leaves  {{ tex \mathrel{\boxtimes} }}
  | tt           ::   :: tt   {{ tex \mathop{ \textbf ! } }}
  | fst          ::   :: fst  {{ tex \pi{}_1 }}
  | snd          ::   :: snd  {{ tex \pi{}_2 }}
  | true         ::   :: true  {{ tex \top }}
  | false        ::   :: false  {{ tex \bot }}
  | /\           ::   :: and  {{ tex \wedge }}
  | \/           ::   :: or  {{ tex \vee }}
  | lam          ::   :: lam  {{ tex \lambda{} }}
  | epsilon      ::   :: epsilon  {{ tex \varepsilon{} }}
  | nil          ::   :: nil   {{ tex \bullet{} }}
  | let          ::   :: let  {{ tex \mathbin{\textbf{let} } }}
  | in           ::   :: letin  {{ tex \mathbin{\textbf{in} } }}
  | \in          ::   :: member  {{ tex \mathrel{\in} }}
  | \infer       ::   :: infer  {{ tex \Rightarrow }}
  | \check       ::   :: check  {{ tex \Leftarrow }}
  | !            ::   :: big     {{ tex \mathrel{\Downarrow} }}
  | !=           ::   :: ne     {{ tex \mathrel{\ne} }}

% FIXME get rid of ?
funs
  eta_expand ::=
fun
  eta ( t , V ) :: v :: eta {{ tex \eta_{[[t]]} [[V]] }}
by
  eta ( A , V ) === neu( V )
  eta ( unit , V ) === tt
  eta (t1 * t2 , V ) === eta ( t1 , (fst V)), eta ( t2 , (snd V))

funs
  inject_nf ::=
fun
  [ v ] :: E :: inject
by
  [ neu ( x ) ] === x
  [ tt ] === tt
  [ v , v' ] === [v] , [v']
  [ f v ] === f [v]
  [ v ] === tt

funs
  useallof ::=
fun
  useall G :: D :: useall
by
  useall nil === nil
  useall (G , x: t) === useall G , x [used]

funs
  usenoneof ::=
fun
  usenone G :: D :: usenone
by
  usenone nil === nil
  usenone (G , x: t) === usenone G , x [unused]

defns

find :: '' ::=

defn
x : t \in G ::   :: mem :: 'mem_'
by

----------------- :: eq
x: t \in G, x : t

x != y
x: t \in G
------------------ :: ne
x: t \in G, y : t'

defns

judge_type :: 'J' ::=

defn
S |- t ::   :: t :: 't_'
by

{{ Assoc.find [[A]] [[S]] = Some tt }}
------- :: var
S |- A

--------- :: unit
S |- unit

S |- t
S |- t'
----------- :: prod
S |- t * t'

defns

judge_functions :: 'J' ::=

defn
S |- FS ::   :: f :: 'f_'
by

--------- :: nil
S |- nil

{{ Assoc.find [[A]] [[S]] = Some tt }}
S |- t
S |- FS
-------------------- :: cons
S |- FS, f : t -> A

defns

judge_relations :: 'J' ::=

defn
S |- RS ::   :: R :: 'R_'
by

-------- :: nil
S |- nil

S |- t
S |- RS
--------------- :: cons
S |- RS, R : t

defns

judge_term :: 'J' ::=

defn
S ; FS ; G |- V \infer t ::   :: V :: 'V_'
by

x: t \in G
------------------------ :: var
S ; FS ; G |- x \infer t

S ; FS ; G |- V \infer t1 * t2
------------------------------- :: fst
S ; FS ; G |- fst V \infer t1

S ; FS ; G |- V \infer t1 * t2
-------------------------------- :: snd
S ; FS ; G |- snd V \infer t2

defn
S ; FS ; G |- v \check t ::   :: v :: 'v_'
by

{{ Assoc.find [[f]] [[FS]] = Some ([[t]], [[A]]) }}
S ; FS ; G |- v \check t
--------------------------- :: function
S ; FS ; G |- f v \check A

---------------------------- :: tt
S ; FS ; G |- tt \check unit

S ; FS ; G |- v1 \check t1
S ; FS ; G |- v2 \check t2
----------------------------------- :: fanout
S ; FS ; G |- v1, v2 \check t1 * t2

S ; FS ; G |- V \infer A
-------------------------------- :: neu
S ; FS ; G |- neu ( V ) \check A

defns

judge_subst :: 'J' ::=

defn
S ; FS |- p : G -> G' ::   :: p :: 'p_'
by

------------------------ :: bang
S ; FS |- nil: G -> nil

S ; FS ; G1 |- v \check t
S ; FS |- p : G1 -> G2
------------------------------------- :: cut
S ; FS |- p, x := v : G1 -> G2, x: t

defns

bigV :: '' ::=

defn
p |- V ! v' ::   :: bigV :: 'bigV_'
by

{{ Assoc.find [[x]] [[p]] = Some [[v]] }}
----------------------------------------- :: var
p |- x ! v

p |- V ! v1, v2
----------------- :: fst
p |- fst V ! v1

p |- V ! v1, v2
----------------- :: snd
p |- snd V ! v2

defns

bigv :: '' ::=

defn
p |- v ! v' ::   :: bigv :: 'bigv_'
by

p |- v ! v'
--------------- :: function
p |- f v ! f v'

------------- :: tt
p |- tt ! tt

p |- v1 ! v1'
p |- v2 ! v2'
---------------------- :: fanout
p |- v1, v2 ! v1', v2'

p |- V ! v
------------------ :: neu
p |- neu( V ) ! v

defns

lfind :: '' ::=

defn
x \in D # D' ::   :: lmem :: 'lmem_'
by

-------------------------------------- :: eq
x \in D, x [ unused ] # D, x [ used ]

x != y
x \in D # D'
------------------------------ :: ne
x \in D, y [ u ] # D', y [ u ]

defns

scope :: '' ::=

defn
  D |- c # D' ::   :: se :: 'se_'
by

D |- E # D'
------------- :: relation
D |- R E # D'

D1 |- E # D2
D2 |- E' # D3
--------------------- :: unify
D1 |- E = E' : t # D3

-------------- :: false
D |- false # D

% FIXME Not sure if and/or should be nonlinear?

D1 |- c # D2
D1 |- c' # D2
------------------- :: or
D1 |- c \/ c' # D2

defn
  D |- E # D' ::   :: sE :: 'sE_'
by

x \in D # D'
----------- :: var
D |- x # D'

D |- E # D'
------------- :: function
D |- f E # D'

D, x[unused] |- c # D', x[used]
-------------------------------- :: epsilon
D |- epsilon x . c # D'

------------ :: tt
D |- tt # D

D1 |- E1 # D2
D2 |- E2 # D3
----------------- :: fanout
D1 |- E1, E2 # D3

D1 |- c # D2
---------------------- :: match_tt
D1 |- epsilon. c # D2

D1, x [unused], y[unused] |- c # D2, x[used], y[used]
----------------------------------------------------- :: match_fanout
D1 |- epsilon x , y . c # D2


D1 |- E # D2
----------------- :: dup
D1 |- dup E # D2

D1 |- E # D2
-------------------- :: del
D1 |- del E : t # D2

defns

judge_context :: '' ::=

defn
S ; FS ; RS ; G |- c ::   :: infer :: 'infer_'
by

{{ Assoc.find [[R]] [[RS]] = Some [[t]] }}
S ; FS ; RS ; G |- E \check t
------------------------------- :: relation
S ; FS ; RS ; G |- R E

S ; FS ; RS ; G |- E \check t
S ; FS ; RS ; G |- E' \check t
----------------------------- :: unify
S ; FS ; RS ; G |- E = E' : t

------------------------- :: false
S ; FS ; RS ; G |- false

S ; FS ; RS ; G |- c
S ; FS ; RS ; G |- c'
---------------------------- :: or
S ; FS ; RS ; G |- c \/ c'

defn
S ; FS ; RS ; G |- E \check t ::   :: check :: 'check_'
by

{{ Assoc.find [[x]] [[G]] = Some [[t]] }}
----------------------------- :: var
S ; FS ; RS ; G |- x \check t

{{ Assoc.find [[f]] [[FS]] = Some ([[t]], [[A]]) }}
S ; FS ; RS ; G |- E \check t
------------------------------------ :: function
S ; FS ; RS ; G |- f E \check A

{{ Assoc.find [[A]] [[S]] = Some tt }}
S ; FS ; RS ; G, x: A |- c
----------------------------------------- :: epsilon
S ; FS ; RS ; G |- epsilon x . c \check A

--------------------------------- :: tt
S ; FS ; RS ; G |- tt \check unit

S ; FS ; RS ; G |- E1 \check t1
S ; FS ; RS ; G |- E2 \check t2
---------------------------------------- :: fanout
S ; FS ; RS ; G |- E1, E2 \check t1 * t2

S ; FS ; RS ; G |- c
----------------------------------------- :: match_tt
S ; FS ; RS ; G |- epsilon. c \check unit

S ; FS ; RS ; G, x: t1, y: t2 |- c
--------------------------------------------------- :: match_fanout
S ; FS ; RS ; G |- epsilon x , y . c \check t1 * t2

S ; FS ; RS ; G |- E \check t
------------------------------------- :: dup
S ; FS ; RS ; G |- dup E \check t * t

S ; FS ; RS ; G |- E \check t
---------------------------------------- :: del
S ; FS ; RS ; G |- del E : t \check unit

defns

judge_sequent :: 'J' ::=

defn
S ; FS ; RS |- H ::   :: H :: 'H_'
by

% FIXME check wellformed environment ?
S ; FS ; RS ; G |- c
S ; FS ; RS ; G |- c'
usenone G |- c # useall G
usenone G |- c' # useall G
----------------------------- :: sequent
S ; FS ; RS |- Pi G . c => c'

defns

judge_theory :: 'J' ::=

defn
S ; FS ; RS |- T ::   :: T :: 'T_'
by

------------------ :: nil
S ; FS ; RS |- nil

S ; FS ; RS |- H
S ; FS ; RS |- T
-------------------- :: cons
S ; FS ; RS |- T, H

defns

pfind :: '' ::=

defn
x := v \in p # p' ::   :: pmem :: 'pmem_'
by

--------------------------------- :: eq
x := v \in p, x := v # p, x := tt

x != y
x := v \in p # p'
------------------------------------- :: ne
x := v \in p, y := v' # p', y := v'


defns

sat :: '' ::=

defn
  T ; p |- c # p' ::   :: produces :: 'produces_'
by

T ; p1 |- E [v] # p2
T ; p2 |- E' [v] # p3
----------------------------- :: unify
T ; p1 |- E = E' : t # p3

T ; p |- c # p'
-------------------------- :: or_inl
T ; p |- c \/ c' # p'

T ; p |- c' # p'
-------------------------- :: or_inr
T ; p |- c \/ c' # p'

defn
  T ; p |- E [ v ] # p' ::   :: accepts :: 'accepts_'
by

x := v \in p # p'
--------------------- :: var
T ; p |- x [ v ] # p'

T ; p |- E [v] # p'
----------------------- :: function
T ; p |- f E [f v] # p'

--------------------- :: tt
T ; p |- tt [tt] # p

T ; p1 |- E [v] # p2
T ; p2 |- E' [v'] # p3
------------------------------- :: fanout
T ; p1 |- (E, E') [v, v'] # p3

T ; p1 |- c # p2
-------------------------------- :: match_tt
T ; p1 |- epsilon. c [ tt ] # p2

T ; p1, x := v0, y := v1 |- c # p2, x := v0', y := v1'
------------------------------------------------------ :: match_fanout
T ; p1 |- epsilon x , y . c [ v0, v1 ] # p2

T ; p1 |- E [v] # p2
----------------------------- :: del
T ; p1 |- del E : t [tt] # p2

T ; p1 |- E [v] # p2
---------------------------- :: dup
T ; p1 |- dup E [v, v] # p2

T ; p1, x := v |- c # p2, x := v'
------------------------------------- :: epsilon
T ; p1 |- (epsilon x . c) [ v ] # p2

