embed
{{ coq-preamble
Require Blech.Assoc.
}}
metavar var, x, y ::= {{ coq nat }} {{ coq-equality }} {{ lex alphanum}} {{ tex \mathit{[[var]]} }}
metavar axiom, K, A ::= {{ coq nat }} {{ coq-equality }} {{ lex alphanum}} {{ tex \mathit{[[axiom]]} }}

grammar
type, t {{ tex \tau }} {{ coq τ }} :: 't_' ::= {{ coq-equality }}
  | A            ::   :: var
  | unit         ::   :: unit
  | t * t'       ::   :: prod
  | ( t )        :: S :: paren {{ coq ([[t]]) }}

use, u :: 'u_' ::= {{ coq-equality }}
  | used   ::  :: used
  | unused ::  :: unused

usage, D {{ tex \Delta }} {{ coq Δ }} :: 'D_' ::= {{ coq (Assoc.assoc use) }}
  | nil                  ::   :: nil {{ coq nil }}
  | D , x [ u ]          ::   :: cons {{ coq (cons ([[x]], [[u]]) [[D]]) }}

global, g :: 'g_' ::=
  | sort              ::   :: sort
  | t -> A            ::   :: function
  | t -/> A           ::   :: relation

signature, S {{ tex \Sigma }} {{ coq Σ }} :: 'S_' ::= {{ coq (Assoc.assoc global) }}
  | nil         ::   :: nil {{ coq nil }}
  | S , K : g       ::   :: add {{ coq (cons ([[K]], [[g]]) [[S]]) }}

% FIXME put sequents here
sequent, H :: 'H_' ::=
  | E [ v ] => E' [ v' ] ::  :: seq

theory, T :: 'T_' ::= {{ coq (list sequent) }}
  | nil         ::   :: nil {{ coq nil }}
  | T , H       ::   :: add {{ coq (cons [[H]] [[T]]) }}

environment, G {{ tex \Gamma }} {{ coq Γ }} :: 'G_' ::= {{ coq (Assoc.assoc type) }}
  | nil         ::   :: nil {{ coq nil }}
  | G , x : t   ::   :: add {{ coq (cons ([[x]], [[t]]) [[G]]) }}
  | ( G )       :: S :: paren {{ coq ([[G]]) }}
  | G ++ G'     :: M :: app {{ coq ([[G]] ++ [[G']]) }}

elim, V :: 'V_' ::= {{ coq-equality }}
  | x              ::   :: var
  | fst V          ::   :: fst
  | snd V          ::   :: snd
  | ( V )          :: S :: paren {{ icho ([[V]]) }}
intro, v :: 'v_' ::= {{ coq-equality }}
  | K v           ::   :: axiom
  | tt            ::   :: tt
  | v , v'        ::   :: fanout
  | neu ( V )     ::   :: neu {{ tex [ [[V]] ] }}
  | ( v )         :: S :: paren {{ icho ([[v]]) }}

context, E :: 'E_' ::=
  | x                        ::   :: var
  | K E                      ::   :: axiom
  | inj v                    ::   :: inj
  | epsilon x : t . c        ::   :: epsilon (+ bind x in c +)
  | tt                       ::   :: tt
  | E , E'                   ::   :: fanout
  | ( E )                    :: S :: paren {{ icho ([[E]]) }}

command, c :: 'c_' ::=
  | < E | E' >               ::   :: unify {{ tex \langle [[E]] \mid [[E']] \rangle }}
  | true                     ::   :: true
  | false                    ::   :: false
  | c /\ c'                  ::   :: and
  | c \/ c'                  ::   :: or
  | E ; c                    ::   :: step
  | let x , y = E in c       ::   :: let (+ bind x union y in c +)
  | ( c )                    :: S :: paren {{ icho ([[c]]) }}

subst, p {{ tex \rho }} {{ coq ρ }} :: 'p_'  ::= {{ coq (Assoc.assoc intro) }}
  | nil                   :: M :: nil {{ coq nil }}
  | p , x := v            :: M :: add {{ coq (cons ([[x]], [[v]]) [[p]]) }}
  | ( p )                 :: S :: paren {{ coq ([[p]]) }}

span, P :: 'P_' ::=
  | p |- v  ::   :: with

spans, Ps {{ tex P^* }} :: 'Ps_' ::= {{ coq (list span) }}
  | nil          ::   :: nil {{ coq nil }}
  | Ps , P       ::   :: cons {{ coq (cons [[P]] [[Ps]]) }}

formula :: formula_ ::=  
 | judgement                        ::   :: judgement
 | x != y                           :: M :: neqx {{ icho ([[x]] <> [[y]]) }}
 | x = y                            :: M :: eqx {{ icho ([[x]] = [[y]]) }}
 | n = n'                           :: M :: eqn {{ icho ([[n]] = [[n']]) }}

terminals :: 'terminals_' ::=
  | :            ::   :: in     {{ tex \colon{} }}
  | |-           ::   :: turnstile   {{ tex \vdash{} }}
  | :=           ::   :: update  {{ tex \mathbin{:=} }}
  | ->           ::   :: to  {{ tex \rightarrow }}
  | -/>          ::   :: rel  {{ tex \nrightarrow }}
  | &            ::   :: act  {{ tex \cdot }}
  | @            ::   :: compose  {{ tex \circ }}
  | unit         ::   :: unit  {{ tex \textbf{I} }}
  | sort         ::   :: sort  {{ tex \Box }}
  | *            ::   :: prod  {{ tex \otimes{} }}
  | used         ::   :: used  {{ tex \textbf{s} }}
  | unused       ::   :: unused  {{ tex \textbf{f} }}
  | #            ::   :: leaves  {{ tex \mathrel{\boxtimes} }}
  | tt           ::   :: tt   {{ tex \mathop{ \textbf ! } }}
  | fst          ::   :: fst  {{ tex \pi{}_1 }}
  | snd          ::   :: snd  {{ tex \pi{}_2 }}
  | true         ::   :: true  {{ tex \top }}
  | false        ::   :: false  {{ tex \bot }}
  | /\          ::   :: and  {{ tex \wedge }}
  | \/           ::   :: or  {{ tex \vee }}
  | lam          ::   :: lam  {{ tex \lambda{} }}
  | epsilon           ::   :: epsilon  {{ tex \varepsilon{} }}
  | nil          ::   :: nil   {{ tex \bullet{} }}
  | let          ::   :: let  {{ tex \mathbin{\textbf{let} } }}
  | in           ::   :: letin  {{ tex \mathbin{\textbf{in} } }}
  | \in          ::   :: member  {{ tex \mathrel{\in} }}
  | \infer       ::   :: infer  {{ tex \Rightarrow }}
  | \check       ::   :: check  {{ tex \Leftarrow }}
  | once         ::   :: once  {{ tex \mathrel{\in!} }}
  | never        ::   :: never  {{ tex \mathrel{\notin} }}
  | ++           ::   :: merge  {{ tex \mathrel{\cup} }}
  | !            ::   :: big     {{ tex \mathrel{\Downarrow} }}
  | !=           ::   :: ne     {{ tex \mathrel{\ne} }}
  | ==           ::   :: equiv   {{ tex \mathrel{\sim} }}

funs
  eta_expand ::=
fun
  eta ( t , V ) :: v :: eta {{ tex \eta_{[[t]]} [[V]] }}
by
  eta ( A , V ) === neu( V )
  eta ( unit , V ) === tt
  eta (t1 * t2 , V ) === eta ( t1 , (fst V)), eta ( t2 , (snd V))

defns

find :: '' ::=

defn
x : t \in G ::   :: mem :: 'mem_'
by

----------------- :: eq
x: t \in G, x : t

x != y
x: t \in G
-------------------- :: ne
x: t \in G, y : t'

defns

judge_term :: 'J' ::=

defn
S [ G ] |- V \infer t ::   :: V :: 'V_'
by

x: t \in G
----------------- :: var
S[G] |- x \infer t

S[G] |- V \infer t1 * t2
------------------ :: fst
S[G] |- fst V \infer t1

S[G] |- V \infer t1 * t2
------------------ :: snd
S[G] |- snd V \infer t2

defn
S [ G ] |- v \check t ::   :: v :: 'v_'
by

{{ Assoc.find [[K]] [[S]] = Some [[t -> A]] }}
S[G] |- v \check t
------------------------------- :: axiom
S[G] |- K v \check A

----------------------- :: tt
S[G] |- tt \check unit

S[G] |- v1 \check t1
S[G] |- v2 \check t2
------------------------------ :: fanout
S[G] |- v1, v2 \check t1 * t2

S[G] |- V \infer A
----------------- :: neu
S[G] |- neu ( V) \check A

defns

judge_subst :: 'J' ::=

defn
S |- p : G -> G' ::   :: p :: 'p_'
by

--------------- :: bang
S |- nil: G -> nil

S[G1] |- v \check t
S |- p : G1 -> G2
----------------------------------- :: cut
S |- p, x := v : G1 -> G2, x: t

defns

bigV :: '' ::=

defn
p |- V ! v' ::   :: bigV :: 'bigV_'
by

{{ Assoc.find [[x]] [[p]] = Some [[v]] }}
------------------------------------------ :: var
p |- x ! v

p |- V ! v1, v2
----------------- :: fst
p |- fst V ! v1

p |- V ! v1, v2
----------------- :: snd
p |- snd V ! v2

defns

bigv :: '' ::=

defn
p |- v ! v' ::   :: bigv :: 'bigv_'
by

p |- v ! v'
------------------------ :: axiom
p |- K v ! K v'

------------- :: tt
p |- tt ! tt

p |- v1 ! v1'
p |- v2 ! v2'
----------------------- :: fanout
p |- v1, v2 ! v1', v2'

p |- V ! v
-------------------- :: neu
p |- neu( V ) ! v

defns

lfind :: '' ::=

defn
x \in D # D' ::   :: lmem :: 'lmem_'
by

-------------------------------------- :: eq
x \in D, x [ unused ] # D, x [ used ]

x != y
x \in D # D'
-------------------------------- :: ne
x \in D, y [ u ] # D', y [ u ]

defns

scope :: '' ::=

defn
  D |- c # D' ::   :: se :: 'se_'
by

D1 |- E # D2
D2 |- E' # D3
------------------ :: unify
D1 |- < E | E' > # D3

D1 |- E # D2
D2 |- c # D3
------------------ :: step
D1 |- E; c # D3

D1 |- E # D2
D2, x [unused], y[unused] |- c # D3, x[used], y[used]
------------------------------------------------------ :: let
D1 |- let x, y = E in c # D3

-------------- :: true
D |- true # D

-------------- :: false
D |- false # D

% FIXME Not sure if and/or should be nonlinear?
D1 |- c # D2
D2 |- c' # D3
------------------- :: and
D1 |- c /\ c' # D3

D1 |- c # D2
D2 |- c' # D3
------------------- :: or
D1 |- c \/ c' # D3

defn
  D |- E # D' ::   :: sE :: 'sE_'
by

x \in D # D'
----------- :: var
D |- x # D'

D |- E # D'
---------------------- :: axiom
D |- K E # D'

D, x[unused] |- c #  D', x[used]
---------------------------------- :: epsilon
D |- epsilon x : t . c # D'

------------ :: tt
D |- tt # D

D1 |- E1 # D2
D2 |- E2 # D3
-------------------- :: fanout
D1 |- E1, E2 # D3

defns

judge_context :: '' ::=

defn
S [ G ] |- c ::   :: infer :: 'infer_'
by

S[G] |- E \check unit
S[G] |- c
-------------- :: step
S[G] |- E; c

S[G] |- E \check t1 * t2
S[G, x: t1, y: t2] |- c
----------------------------- :: let
S[G] |- let x, y = E in c

S[G] |- E \check t
S[G] |- E' \check t
-------------------- :: unify
S[G] |- < E | E' >

------------- :: true
S[G] |- true

-------------- :: false
S[G] |- false

S[G] |- c
S[G] |- c'
---------------- :: and
S[G] |- c /\ c'

S[G] |- c
S[G] |- c'
--------------- :: or
S[G] |- c \/ c'

defn
S [ G ] |- E \check t ::   :: check :: 'check_'
by

{{ Assoc.find [[x]] [[G]] = Some [[t]] }}
----------------------- :: var
S[G] |- x \check t

{{ Assoc.find [[K]] [[S]] = Some [[t -/> A]] }}
S[G] |- E \check t
--------------------------- :: axiom
S[G] |- K E \check A

% S[G] |- v \check t
% ------------------------ :: inject
% S[G] |- inj v \check t


S[G, x: t] |- c
----------------------------------- :: epsilon
S[G] |- epsilon x : t . c \check t

-------------------- :: tt
S[G] |- tt \check unit

S[G] |- E1 \check t1
S[G] |- E2 \check t2
---------------------------- :: fanout
S[G] |- E1, E2 \check t1 * t2

defns

pfind :: '' ::=

defn
x := v \in p # p' ::   :: pmem :: 'pmem_'
by

--------------------------------- :: eq
x := v \in p, x := v # p, x := tt

x != y
x := v \in p # p'
------------------------------------- :: ne
x := v \in p, y := v' # p', y := v'


defns

sat :: '' ::=

defn
  T ; p |- c # p' ::   :: produces :: 'produces_'
by

T ; p1 |- E [ tt ] # p2
T ; p2 |- c # p3
---------------------------- :: step
T ; p1 |- E; c # p3

T ; p1 |- E [v0, v1] # p2
T ; p2, x := v0, y := v1 |- c # p3, x := v0', y := v1'
-------------------------------------------------------------- :: let
T ; p1 |- let x, y = E in c # p3

T ; p1 |- E [v] # p2
T ; p2 |- E' [v] # p3
----------------------------- :: unify
T ; p1 |- < E | E' > # p3

--------------------- :: true
T ; p |- true # p

T ; p1 |- c # p2
T ; p2 |- c' # p3
-------------------------- :: and
T ; p1 |- c /\ c' # p3

T ; p |- c # p'
-------------------------- :: or_inl
T ; p |- c \/ c' # p'

T ; p |- c' # p'
-------------------------- :: or_inr
T ; p |- c \/ c' # p'

defn
  T ; p |- E [ v ] # p' ::   :: accepts :: 'accepts_'
by

x := v \in p # p'
---------------------- :: var
T ; p |- x [ v ] # p'


% {{ Assoc.find [[K]] [[T]] = Some [[E;v => E';v']] }}
% T ; p |- E [v] # p'
% ----------------------- :: axiom
% T ; p |- K E' [v'] # p'

----------------------- :: inject
T ; p |- inj v [v] # p

--------------------- :: tt
T ; p |- tt [tt] # p

T ; p1 |- E [v] # p2
T ; p2 |- E' [v'] # p3
------------------------------- :: fanout
T ; p1 |- (E, E') [v, v'] # p3

T ; p1, x := v |- c # p2, x := v'
------------------------------------------- :: epsilon
T ; p1 |- (epsilon x : t . c) [ v ] # p2

