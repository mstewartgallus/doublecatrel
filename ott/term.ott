embed
{{ coq-preamble
Require Blech.Assoc.
}}
metavar var, x, y ::= {{ coq nat }} {{ coq-equality }} {{ lex alphanum}} {{ tex \mathit{[[var]]} }}

grammar
expr, V :: 'V_' ::= {{ coq-equality }}
  | x              ::   :: var
  | fst V          ::   :: fst
  | snd V          ::   :: snd
  | ( V )          :: S :: paren {{ icho ([[V]]) }}

term, v :: 'v_' ::= {{ coq-equality }}
  | tt             ::   :: tt
  | v , v'         ::   :: fanout
  | neu ( V )      ::   :: neu {{ tex [ [[V]] ] }}
  | ( v )          :: S :: paren {{ icho ([[v]]) }}
grammar

environment, G {{ tex \Gamma }} {{ coq Γ }} :: 'G_' ::= {{ coq (Assoc.assoc type) }}
  | nil         ::   :: nil {{ coq nil }}
  | G , x : t   ::   :: add {{ coq (cons ([[x]], [[t]]) [[G]]) }}
  | ( G )       :: S :: paren {{ coq ([[G]]) }}

subst, p {{ tex \rho }} {{ coq ρ }} :: 'p_'  ::= {{ coq (Assoc.assoc term) }}
  | nil                   :: M :: nil {{ coq nil }}
  | p , x := v            :: M :: add {{ coq (cons ([[x]], [[v]]) [[p]]) }}
  | ( p )                 :: S :: paren {{ coq ([[p]]) }}

funs
  eta_expand ::=
fun
  eta ( t , V ) :: v :: eta {{ tex \eta_{[[t]]} [[V]] }}
by
  eta ( A , V ) === neu( V )
  eta ( unit , V ) === tt
  eta (t1 * t2 , V ) === eta ( t1 , (fst V)), eta ( t2 , (snd V))

defns

dummy :: '' ::=

defn
jdummy ::   :: jdum :: 'jdum_'
by

defns

find :: '' ::=

defn
x : t \in G ::   :: mem :: 'mem_'
by

----------------- :: eq
x: t \in G, x : t

x != y
x: t \in G
-------------------- :: ne
x: t \in G, y : t'

defns

judge_term :: 'J' ::=

defn
G |- V : t ::   :: V :: 'V_'
by

x: t \in G
------------ :: var
G |- x: t

G |- V: t1 * t2
------------------ :: fst
G |- fst V: t1

G |- V: t1 * t2
------------------ :: snd
G |- snd V: t2

defn
G |- v : t ::   :: v :: 'v_'
by

--------------- :: tt
G |- tt: unit

G |- v1: t1
G |- v2: t2
------------------ :: fanout
G |- v1, v2: t1 * t2

G |- V: A
----------------- :: neu
G |- neu ( V) : A

defns

judge_subst :: 'J' ::=

defn
G |- p : G' ::   :: p :: 'p_'
by

-------------- :: nil
G |- nil: nil

G' |- v : t
G' |- p: G
------------------------- :: cons
G' |- p, x := v: G, x: t

defns

hsubstsV :: '' ::=

defn
V & p === v' ::   :: hsubstsV :: 'hsubstsV_'
by

{{ Assoc.find [[x]] [[p]] = Some [[v]] }}
------------------------------------------ :: var
x & p === v

V & p === v1, v2
----------------- :: fst
fst V & p === v1

V & p === v1, v2
----------------- :: snd
snd V & p === v2

defns

hsubstsv :: '' ::=

defn
v @ p === v' ::   :: hsubstsv :: 'hsubstsv_'
by

------------- :: tt
tt @ p === tt

v1 @ p === v1'
v2 @ p === v2'
----------------------- :: fanout
v1, v2 @ p === v1', v2'

V & p === v
-------------------- :: neu
neu( V ) @ p === v
