embed
{{ coq-preamble
Require Blech.Assoc.
}}
metavar tyvar, A ::= {{ coq nat }} {{ coq-equality }} {{ lex alphanum}} {{ tex \mathit{[[tyvar]]} }}
metavar var, x, y ::= {{ coq nat }} {{ coq-equality }} {{ lex alphanum}} {{ tex \mathit{[[var]]} }}
metavar axiom, K ::= {{ coq nat }} {{ coq-equality }} {{ lex alphanum}} {{ tex \mathit{[[axiom]]} }}

grammar
type, t :: 't_' ::= {{ coq-equality }}
  | A            ::   :: var
  | unit         ::   :: unit
  | t * t'       ::   :: prod
  | ( t )        :: S :: paren {{ coq ([[t]]) }}

use, u :: 'u_' ::= {{ coq-equality }}
  | used   ::  :: used
  | unused ::  :: unused

usage, D {{ tex \Delta }} {{ coq Δ }} :: 'D_' ::= {{ coq (Assoc.assoc use) }}
  | nil                  ::   :: nil {{ coq nil }}
  | D , x [ u ]          ::   :: cons {{ coq (cons ([[x]], [[u]]) [[D]]) }}

global, g :: 'g_' ::=
  | t -> A  ::   :: function

globals, X :: 'X_' ::= {{ coq (Assoc.assoc global) }}
  | nil         ::   :: nil {{ coq nil }}
  | X , K : g       ::   :: add {{ coq (cons ([[K]], [[g]]) [[X]]) }}

environment, G {{ tex \Gamma }} {{ coq Γ }} :: 'G_' ::= {{ coq (Assoc.assoc type) }}
  | nil         ::   :: nil {{ coq nil }}
  | G , x : t   ::   :: add {{ coq (cons ([[x]], [[t]]) [[G]]) }}
  | ( G )       :: S :: paren {{ coq ([[G]]) }}
  | G ++ G'     :: M :: app {{ coq ([[G]] ++ [[G']]) }}

elim, V :: 'V_' ::= {{ coq-equality }}
  | x              ::   :: var
  | fst V          ::   :: fst
  | snd V          ::   :: snd
  | ( V )          :: S :: paren {{ icho ([[V]]) }}
intro, v :: 'v_' ::= {{ coq-equality }}
  | K v           ::   :: axiom
  | tt            ::   :: tt
  | v , v'        ::   :: fanout
  | neu ( V )     ::   :: neu {{ tex [ [[V]] ] }}
  | ( v )         :: S :: paren {{ icho ([[v]]) }}

context, E :: 'E_' ::=
  | inj v                    ::   :: inj
  | lam x . E                ::   :: lam (+ bind x in E +)
  | tt                       ::   :: tt
  | E , E'                   ::   :: fanout
  | neu e                    ::   :: neu {{ tex [ [[e]] ] }}
  | ( E )                    :: S :: paren {{ icho ([[E]]) }}
redex, e :: 'e_' ::=
  | x                        ::   :: var
  | e E'                     ::   :: app
  | e ; E' : t               ::   :: step
  | let x , y = e in E' : t  ::   :: let (+ bind x union y in E' +)
  | cut E t                  ::   :: cut {{ tex [[E]] \colon [[t]] }}
  | ( e )                    :: S :: paren {{ icho ([[e]]) }}

subst, p {{ tex \rho }} {{ coq ρ }} :: 'p_'  ::= {{ coq (Assoc.assoc intro) }}
  | nil                   :: M :: nil {{ coq nil }}
  | p , x := v            :: M :: add {{ coq (cons ([[x]], [[v]]) [[p]]) }}
  | ( p )                 :: S :: paren {{ coq ([[p]]) }}

span, P :: 'P_' ::=
  | p |- v  ::   :: with

spans, Ps {{ tex P^* }} :: 'Ps_' ::= {{ coq (list span) }}
  | nil          ::   :: nil {{ coq nil }}
  | Ps , P       ::   :: cons {{ coq (cons [[P]] [[Ps]]) }}

formula :: formula_ ::=  
 | judgement                        ::   :: judgement
 | x != y                           :: M :: neqx {{ icho ([[x]] <> [[y]]) }}
 | x = y                            :: M :: eqx {{ icho ([[x]] = [[y]]) }}
 | n = n'                           :: M :: eqn {{ icho ([[n]] = [[n']]) }}

terminals :: 'terminals_' ::=
  | :            ::   :: in     {{ tex \colon{} }}
  | |-           ::   :: turnstile   {{ tex \vdash{} }}
  | :=           ::   :: update  {{ tex \mathbin{:=} }}
  | ->           ::   :: to  {{ tex \rightarrow }}
  | &            ::   :: act  {{ tex \cdot }}
  | @            ::   :: compose  {{ tex \circ }}
  | unit         ::   :: unit  {{ tex \textbf{I} }}
  | *            ::   :: prod  {{ tex \otimes{} }}
  | used         ::   :: used  {{ tex \textbf{s} }}
  | unused       ::   :: unused  {{ tex \textbf{f} }}
  | #            ::   :: leaves  {{ tex \mathrel{\boxtimes} }}
  | tt           ::   :: tt   {{ tex \mathop{ \textbf ! } }}
  | fst          ::   :: fst  {{ tex \pi{}_1 }}
  | snd          ::   :: snd  {{ tex \pi{}_2 }}
  | lam          ::   :: lam  {{ tex \lambda{} }}
  | nil          ::   :: nil   {{ tex \bullet{} }}
  | let          ::   :: let  {{ tex \mathbin{\textbf{let} } }}
  | in           ::   :: letin  {{ tex \mathbin{\textbf{in} } }}
  | \in          ::   :: member  {{ tex \mathrel{\in} }}
  | \infer       ::   :: infer  {{ tex \Rightarrow }}
  | \check       ::   :: check  {{ tex \Leftarrow }}
  | once         ::   :: once  {{ tex \mathrel{\in!} }}
  | never        ::   :: never  {{ tex \mathrel{\notin} }}
  | ++            ::   :: merge  {{ tex \mathrel{\cup} }}
  | !            ::   :: big     {{ tex \mathrel{\Downarrow} }}
  | !=           ::   :: ne     {{ tex \mathrel{\ne} }}
  | ==           ::   :: equiv   {{ tex \mathrel{\sim} }}

funs
  eta_expand ::=
fun
  eta ( t , V ) :: v :: eta {{ tex \eta_{[[t]]} [[V]] }}
by
  eta ( A , V ) === neu( V )
  eta ( unit , V ) === tt
  eta (t1 * t2 , V ) === eta ( t1 , (fst V)), eta ( t2 , (snd V))

defns

find :: '' ::=

defn
x : t \in G ::   :: mem :: 'mem_'
by

----------------- :: eq
x: t \in G, x : t

x != y
x: t \in G
-------------------- :: ne
x: t \in G, y : t'

defns

judge_term :: 'J' ::=

defn
X [ G ] |- V \infer t ::   :: V :: 'V_'
by

x: t \in G
----------------- :: var
X[G] |- x \infer t

X[G] |- V \infer t1 * t2
------------------ :: fst
X[G] |- fst V \infer t1

X[G] |- V \infer t1 * t2
------------------ :: snd
X[G] |- snd V \infer t2

defn
X [ G ] |- v \check t ::   :: v :: 'v_'
by

{{ Assoc.find [[K]] [[X]] = Some [[t -> A]] }}
X[G] |- v \check t
------------------------------- :: axiom
X[G] |- K v \check A

--------------- :: tt
X[G] |- tt \check unit

X[G] |- v1 \check t1
X[G] |- v2 \check t2
------------------------------ :: fanout
X[G] |- v1, v2 \check t1 * t2

X[G] |- V \infer A
----------------- :: neu
X[G] |- neu ( V) \check A

defns

judge_subst :: 'J' ::=

defn
X |- p : G -> G' ::   :: p :: 'p_'
by

--------------- :: bang
X |- nil: G -> nil

X[G1] |- v \check t
X |- p : G1 -> G2
----------------------------------- :: cut
X |- p, x := v : G1 -> G2, x: t

defns

bigV :: '' ::=

defn
p |- V ! v' ::   :: bigV :: 'bigV_'
by

{{ Assoc.find [[x]] [[p]] = Some [[v]] }}
------------------------------------------ :: var
p |- x ! v

p |- V ! v1, v2
----------------- :: fst
p |- fst V ! v1

p |- V ! v1, v2
----------------- :: snd
p |- snd V ! v2

defns

bigv :: '' ::=

defn
p |- v ! v' ::   :: bigv :: 'bigv_'
by

p |- v ! v'
--------------------------------------- :: axiom
p |- K v ! K v'

------------- :: tt
p |- tt ! tt

p |- v1 ! v1'
p |- v2 ! v2'
----------------------- :: fanout
p |- v1, v2 ! v1', v2'

p |- V ! v
-------------------- :: neu
p |- neu( V ) ! v

defns

lfind :: '' ::=

defn
x \in D # D' ::   :: lmem :: 'lmem_'
by

-------------------------------------- :: eq
x \in D, x [ unused ] # D, x [ used ]

x != y
x \in D # D'
-------------------------------- :: ne
x \in D, y [ u ] # D', y [ u ]

defns

scope :: '' ::=

defn
  D |- e # D' ::   :: se :: 'se_'
by

x \in D # D'
----------- :: var
D |- x # D'

D1 |- e1 # D2
D2 |- E2 # D3
------------------ :: app
D1 |- e1 E2 # D3

D1 |- e1 # D2
D2 |- E2 # D3
----------------------- :: step
D1 |- e1; E2 : t # D3

D1 |- e1 # D2
D2, x [unused], y[unused] |- E2 # D3, x[used], y[used]
------------------------------------------------------ :: let
D1 |- let x, y = e1 in E2 : t3 # D3

D |- E # D'
------------------- :: cut
D |- cut E t # D'

defn
  D |- E # D' ::   :: sE :: 'sE_'
by

D, x[unused] |- E #  D', x[used]
---------------------------------- :: lam
D |- lam x . E # D'

------------ :: tt
D |- tt # D

D1 |- E1 # D2
D2 |- E2 # D3
-------------------- :: fanout
D1 |- E1, E2 # D3

D |- e # D'
------------------- :: neu
D |- neu e # D'

defns

judge_context :: '' ::=

defn
X [ G ]  |- e \infer t ::   :: infer :: 'infer_'
by

x: t \in G
----------------------- :: var
X[G] |- x \infer t

X[G] |- e1 \infer t1 * t2
X[G] |- E2 \check t1
--------------------------------- :: app
X[G] |- e1 E2 \infer t2

X[G] |- e1 \infer unit
X[G] |- E2 \check t
---------------------------------- :: step
X[G] |- e1; E2 : t \infer t

X[G] |- e1 \infer t1 * t2
X[G, x: t1, y: t2] |- E2 \check t3
------------------------------------------------- :: let
X[G] |- let x, y = e1 in E2 : t3 \infer t3

X[G] |- E \check t
---------------------------------- :: cut
X[G] |- cut E t \infer t

defn
X [ G ] |- E \check t ::   :: check :: 'check_'
by

X[G] |- v \check t
---------------------------------------- :: inject
X[G] |- inj v \check t

X[G, x: t1] |- E \check t2
---------------------------------------- :: lam
X[G] |- lam x . E \check t1 * t2

-------------------- :: tt
X[G] |- tt \check unit

X[G] |- E1 \check t1
X[G] |- E2 \check t2
---------------------------- :: fanout
X[G] |- E1, E2 \check t1 * t2

X[G] |- e \infer t
---------------------- :: neu
X[G] |- neu e \check t

defns

pfind :: '' ::=

defn
x := v \in p # p' ::   :: pmem :: 'pmem_'
by

--------------------------------- :: eq
x := v \in p, x := v # p, x := tt

x != y
x := v \in p # p'
------------------------------------- :: ne
x := v \in p, y := v' # p', y := v'


defns

sat :: '' ::=

defn
  p |- e [ v ] # p' ::   :: produces :: 'produces_'
by

x := v \in p # p'
-------------------- :: var
p |- x [ v ] # p'

p1 |- e [ tt ] # p2
p2 |- E' [ v ] # p3
------------------------------- :: step
p1 |- (e; E': t) [ v ] # p3

p1 |- e [v0, v1] # p2
p2, x := v0, y := v1 |- E' [ v2 ] # p3, x := v0', y := v1'
----------------------------------------------------------- :: let
p1 |- (let x, y = e in E' : t) [ v2 ] # p3

p1 |- e [ v, v' ] # p2
p2 |- E' [v] # p3
----------------------------- :: app
p1 |- (e E') [ v' ] # p3

p1 |- E [ v ] # p2
----------------------------- :: cut
p1 |- (cut E t) [ v ] # p2

defn
  p |- E [ v ] # p' ::   :: accepts :: 'accepts_'
by

-------------------- :: inject
p |- inj v [v] # p

--------------- :: tt
p |- tt [tt] # p

p1 |- E [v] # p2
p2 |- E' [v'] # p3
--------------------------- :: fanout
p1 |- (E, E') [v, v'] # p3

p1, x := v1 |- E [ v2 ] # p2, x := v1'
-------------------------------------- :: lam
p1 |- (lam x. E) [ v1, v2 ] # p2

p1 |- e [ v ] # p2
--------------------------- :: neu
p1 |- (neu e) [ v ] # p2
