embed
{{ coq-preamble
Require Blech.Map.
}}
grammar

context, E :: 'E_' ::= {{ coq-equality }}
  | x                        ::   :: var
  | lam x : t . E            ::   :: lam (+ bind x in E +)
  | E E'                     ::   :: app
  | tt                       ::   :: tt
  | E ; E'                   ::   :: step
  | E , E'                   ::   :: fanout
  | let x , y = E in E'      ::   :: let (+ bind x union y in E' +)
  | ( E )                    :: S :: paren {{ icho ([[E]]) }}

store, S {{ tex \sigma }} {{ coq Ïƒ }} :: 'S_'  ::= {{ coq (Map.map normal) }}
  | nil                   :: M :: empty {{ coq (Map.empty) }}
  | x := N                :: M :: one {{ coq (Map.one [[x]] [[N]]) }}
  | S , S'                :: M :: add {{ coq (Map.merge [[S']] [[S]]) }}
  | S ++ S'               :: M :: merge {{ coq (Map.merge [[S]] [[S']]) }}

span, P :: 'P_' ::=
  | S |- N  ::   :: with

set, Ps {{ tex P^* }} :: 'X_' ::= {{ coq (list span) }}
  | nil          ::   :: nil {{ coq nil }}
  | Ps , P       ::   :: cons {{ coq (cons [[P]] [[Ps]]) }}

terminals :: 'terminals_' ::=
  | :            ::   :: in     {{ tex \colon{} }}
  | |-           ::   :: turnstile   {{ tex \vdash{} }}
  | :=           ::   :: update  {{ tex \mathbin{:=} }}
  | ++           ::   :: merge   {{ tex \cup{} }}
  | unit         ::   :: unit  {{ tex \textbf{I} }}
  | *            ::   :: prod  {{ tex \otimes{} }}
  | tt           ::   :: tt   {{ tex \mathop{ \textbf ! } }}
  | fst          ::   :: fst  {{ tex \pi{}_1 }}
  | snd          ::   :: snd  {{ tex \pi{}_2 }}
  | lam          ::   :: lam  {{ tex \lambda{} }}
  | nil          ::   :: nil   {{ tex \bullet{} }}
  | let          ::   :: let  {{ tex \mathbin{\textbf{let} } }}
  | in           ::   :: letin  {{ tex \mathbin{\textbf{in} } }}
  | \in          ::   :: member  {{ tex \in{} }}
  | once         ::   :: once  {{ tex \mathrel{\in{}!} }}
  | never        ::   :: never  {{ tex \mathrel{\notin} }}
  | !            ::   :: big     {{ tex \mathrel{\Downarrow} }}
  | !=           ::   :: ne     {{ tex \mathrel{\ne} }}
  | ==           ::   :: equiv   {{ tex \mathrel{\sim} }}

parsing
  S_add left S_add

substitutions
  single E x :: subst

grammar
formula :: formula_ ::=  
 | judgement                        ::   :: judgement
 | x != y                           :: M :: neq {{ icho ([[x]] <> [[y]]) }}

defns

nev :: '' ::=

defn
x never E :: :: never :: 'never_'
by

x != y
--------- :: var
x never y

--------------------- :: lam_eq
x never lam x : t. E

x never E
x != y
--------------------- :: lam
x never lam y : t. E

x never E
x never E'
------------ :: app
x never E E'

--------- :: tt
x never tt

x never E
x never E'
------------- :: step
x never E; E'

x never E
x never E'
------------- :: fanout
x never E, E'

x never E
x never E'
x != y
x != y'
---------------------------- :: let
x never let y, y' = E in E'

x never E
---------------------------- :: let_eq_1
x never let x, y = E in E'

x never E
---------------------------- :: let_eq_2
x never let y, x = E in E'

defns

onc :: '' ::=

defn
x once E :: :: once :: 'once_'
by

--------- :: var
x once x

x != y
x once E
------------------- :: lam
x once lam y: t. E

x once E
x never E'
------------- :: app_l
x once E E'

x never E
x once E'
------------- :: app_r
x once E E'

x once E
x never E'
------------- :: step_l
x once E; E'

x never E
x once E'
------------------- :: step_r
x once E; E'

x once E
x never E'
------------- :: fanout_l
x once E, E'

x never E
x once E'
------------------- :: fanout_r
x once E, E'

x once E
x never E'
x != y
x != y'
----------------------------- :: let_l
x once let y, y' = E in E'

x once E
---------------------------- :: let_l1
x once let x, y = E in E'

x once E
----------------------------- :: let_l2
x once let y, x = E in E'

x never E
x once E'
x != y
x != y'
----------------------------- :: let_r
x once let y, y' = E in E'

defns

judge_context :: 'J' ::=

defn
G |- E : t ::   :: E :: 'E_'
by

x: t \in G
------------ :: var
G |- x: t

G, x: t1 |- E: t2
x once E
---------------------------- :: lam
G |- lam x: t1 . E: t1 * t2

G |- E1: t1 * t2
G |- E2: t1
------------------ :: app
G |- E1 E2: t2

---------------- :: tt
G |- tt: unit

G |- E1: unit
G |- E2: t
------------------- :: step
G |- E1; E2: t

G |- E1: t1
G |- E2: t2
------------------- :: fanout
G |- E1, E2: t1 * t2

x once E2
y once E2
G |- E1 : t1 * t2
G, x: t1, y: t2 |- E2: t3
------------------------------------- :: let
G |- let x, y = E1 in E2: t3

defns

sat :: '' ::=

defn
S |- E [ N ] ::   :: sat :: 'sat_' {{ tex [[S]] \mathrel{\vdash} [[E]][ [[N]] ] }}
by

---------------------- :: var
x := N |- x [ N ]

------------------ :: tt
nil |- tt [tt]

S |- E [ tt ]
S' |- E' [ N ]
-------------------------- :: step
S ++ S' |- (E; E') [ N ]

S |-  E [N]
S' |-  E' [N']
-------------------------- :: fanout
S ++ S' |- (E, E') [N, N']

S |- E [N0, N1]
S', x := N0, y := N1 |- E' [ N2 ]
------------------------------------- :: let
S ++ S' |- (let x, y = E in E') [ N2 ]

S, x := N |- E [ N' ]
------------------------- :: lam
S |- (lam x: t. E) [ N, N' ]

S |- E [ N, N' ]
S' |- E' [N]
--------------------- :: app
S ++ S' |- (E E') [ N' ]

defns

sound :: '' ::=

defn
E [ Ps ] ::   :: sound :: 'sound_'
by

----------- :: nil
E [ nil ]

E [ Ps ]
S |- E [N]
---------------- :: cons
E [ Ps, S |- N ]
